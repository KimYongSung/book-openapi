plugins {
	id 'org.springframework.boot' version '2.1.9.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'com.moowork.node' version '1.3.1'
	id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
	id 'java'
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {

	compileOnly(
			'org.projectlombok:lombok'
	)

	runtimeOnly(
			'com.h2database:h2'
	)

	implementation(
			'org.springframework.boot:spring-boot-starter-web'
			,'org.springframework.boot:spring-boot-starter-freemarker'
			,'org.springframework.boot:spring-boot-starter-data-jpa'
			,'org.springframework.boot:spring-boot-starter-security'
			,'org.springframework.boot:spring-boot-devtools'
			,'io.jsonwebtoken:jjwt:0.9.1'
			,'com.querydsl:querydsl-apt'
			,'com.querydsl:querydsl-jpa'
	)

	testImplementation(
			'org.springframework.boot:spring-boot-starter-test'
			,'org.springframework.security:spring-security-test'
	)

	annotationProcessor(
			'com.querydsl:querydsl-apt'
			,'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final'
			,'org.springframework.boot:spring-boot-configuration-processor'
			,'org.projectlombok:lombok'
	)
}

// QueryDSL 설정
def querydslSrcDir = 'src/main/generated'

querydsl {
	library = "com.querydsl:querydsl-apt"
	jpa = true
	querydslSourcesDir = querydslSrcDir
}

compileQuerydsl{
	options.annotationProcessorPath = configurations.querydsl
}

configurations {
	querydsl.extendsFrom compileClasspath
}

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java', querydslSrcDir]
		}
	}
}

// Node 설정
node {
	version = '13.6.0'
	npmVersion = '6.13.1'
	download = false
	workDir = file("./src/frontend")
	nodeModulesDir = file("./src/frontend")
}

// npmInstall 실행 Config
npmInstall {
	args = ['install']
	inputs.files file('package.json')
	outputs.files file('node_modules')
}

task viewBuild(type: NpmTask, dependsOn: npmInstall){
	description = "Build vue.js"
	args = ['run','build']
}

processResources.dependsOn 'viewBuild'